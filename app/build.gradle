buildscript {
    apply from: '../_buildsystem/buildscript.gradle'
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath project.ext.classpathDependencies
    }
}

apply from: '../_buildsystem/buildscript.gradle'
apply from: '../_buildsystem/configurations.gradle'
apply from: '../_buildsystem/dependencies.gradle'
apply from: '../_buildsystem/properties.gradle'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jmailen.kotlinter'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.google.firebase.firebase-crash'

android {
    compileSdkVersion project.ext.androidCompileSdkVersion
    buildToolsVersion project.ext.androidBuildToolsVersion
    signingConfigs {
        dummy1 {
            storeFile new File("dummy1.keystore")
            storePassword("dummy1")
            keyAlias("dummy1")
            keyPassword("dummy1")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug" // Do not change as Firebase depends on this
        }
        release {
            signingConfig signingConfigs.dummy1
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard/rules.pro'
            shrinkResources true
        }
    }
    defaultConfig {
        applicationId project.ext.androidApplicationIdBase
        versionCode project.ext.androidVersionCode
        versionName project.ext.androidVersionName
        minSdkVersion project.ext.androidMinSdkVersion
        targetSdkVersion project.ext.androidTargetSdkVersion
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "8g"
    }
    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$project.ext.staticAnalysisReportTarget/$project.name/lint.html")
        quiet false
        warningsAsErrors true
    }
    sourceSets.forEach {
        it.java.srcDirs = ["src/$it.name/kotlin"]
    }
}
repositories {
    google()
    jcenter()
}
dependencies {
    implementation project(':data') // This is only for the ContentProvider. Don't import data in src!
    implementation project(':domain')
    implementation project(':crash-reporter')
    implementation project(':event-tracker') // Required by the ContentProvider
    implementation project.ext.compileDependencies
    kapt project.ext.annotationProcessorAppDependencies
    implementation project.ext.compileAppDependencies
}

apply plugin: 'com.google.gms.google-services'
setProperty("FirebaseServiceAccountFilePath", project.rootDir.absolutePath + "/" +
        project.ext.firebaseServiceAccountFileName)
