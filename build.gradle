import java.nio.file.Paths

apply from: 'buildsystem/dependencies.gradle'

buildscript {
	repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    final def androidPluginVersion = "3.1.0-alpha01"
    final def kotlinPluginVersion = rootProject.ext.kotlinVersion = "1.1.51"
    final def dokkaPluginVersion = "0.9.15"
    final def googleServicesPluginVersion = "3.1.0"
    final def firebasePluginsVersion = "1.1.0"
    final def kotlinterPluginVersion = "1.2.0"
    final def classpathDependencies = [
            "com.android.tools.build:gradle:$androidPluginVersion",
            "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinPluginVersion",
            "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaPluginVersion",
            "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaPluginVersion",
            "com.google.gms:google-services:$googleServicesPluginVersion",
            "com.google.firebase:firebase-plugins:$firebasePluginsVersion",
            "gradle.plugin.org.jmailen.gradle:kotlinter-gradle:$kotlinterPluginVersion"
    ]
    dependencies {
        classpath classpathDependencies
    }
    rootProject.ext.supportLibraryVersion = "27.0.0"
    rootProject.ext.archRuntimeVersion = "1.0.3"
}
final def inducedVersion = System.getenv("ARTIFACT_VERSION")
final def staticAnalysisReportFolderTarget = project.rootDir.absolutePath + "/staticAnalysisReport"
rootProject.ext {
    androidCompileSdkVersion = 27
    androidMinSdkVersion = 16
    androidTargetSdkVersion = 27
    androidBuildToolsVersion = '27.0.0'
	androidVersionCode = inducedVersion != null ? Integer.parseInt(inducedVersion) : 1 // Depending on the git method locally causes IDE issues
	androidVersionName = String.valueOf(androidVersionCode)
	androidApplicationIdBase = "org.stoyicker.dinger"
	staticAnalysisReportTarget = staticAnalysisReportFolderTarget
    javaVersion = JavaVersion.VERSION_1_7
    firebaseServiceAccountFileName = "firebase-crashreporting-private-key.json"
}
task clean(type: Delete, overwrite: true) {
    group "Verification"
    description "Deletes the build directories of the whole project and the static analysis result."
    rootProject.allprojects.each {
        delete it.buildDir
  	    delete Paths.get(it.projectDir.absolutePath, staticAnalysisReportFolderTarget).toAbsolutePath().toString()
  }
}
